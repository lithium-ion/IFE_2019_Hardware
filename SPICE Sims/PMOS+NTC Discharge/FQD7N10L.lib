* FQD7N10L 100V N-CHANNEL DMOSFET ELECTRICAL PARAMETERS
*------------------------------------------------------------------------------------
.SUBCKT  FQD7N10L   20  10  30
Rg     10    1    1
M1      2    1    3    3    DMOS    L=1u   W=1u
.MODEL DMOS NMOS (VTO={1.75*{-0.002*TEMP+1.05}} KP={4.25*{-0.0002*TEMP+1.005}} 
+ THETA=0.08   VMAX=6.05E5   LEVEL=3)
Cgs     1    3    208P
Rd     20    4    110m   TC=0.0153
Dds     3    4    DDS
.MODEL DDS   D(BV={100*{0.00096*TEMP+0.976}}  M=0.5      CJO=43p   VJ=0.8)
Dbody   3   20    DBODY
.MODEL DBODY D(IS=9.0E-13    N=1.0     RS=40.0m    EG=1.19    TT=70n)
Ra      4    2    90m    TC=0.0153
Rs      3    5    7m    
Ls      5   30    2.3n
M2      1    8    6    6    INTER
E2      8    6    4    1    2
.MODEL INTER NMOS (VTO=0   KP=10   LEVEL=1)
Cgdmax  7    4    450p
Rcgd    7    4    10meg
Dgd     6    4    DGD
Rdgd    4    6    10meg
.MODEL DGD   D(M=0.8   CJO=450p    VJ=0.6)
M3      7    9    1    1    INTER
E3      9    1    4    1    -2
.ENDS FQD7N10L
*-------------------------------------------------------------------------------------
*FAIRCHILD      CASE: D-PAK    PID: FQD7N10L 
*APR-26-2002 CREATION    
*FQD17P06 60V P-CHANNEL DMOSFET ELECTRICAL PARAMETERS
*------------------------------------------------------------------------------------
.SUBCKT  FQD17P06   20  10  30
Rg     10    1    1
M1      2    1    3    3    DMOS    L=1u   W=1u
.MODEL DMOS PMOS (VTO={-3.5*{-0.00034*TEMP+1.0085}} KP={9.3*{-0.000014*TEMP+1.00035}} 
+ THETA=0.056    VMAX=1.95E5   LEVEL=3)
Cgs 1   3   610P
Rd  20  4   42m     TC=0.0042
Dds 4   3   DDS
.MODEL DDS D(BV={60*{0.0008*TEMP+0.98}}  M=0.5      CJO=235p   VJ=0.8)
Dbody   20  3   DBODY
.MODEL DBODY D(IS=9.5E-13     N=1.0     RS=23.3m    EG=1.17    TT=92n)
Ra 4    2   31.3m   TC=0.0042
Rs 3    5   3.2m    
Ls 5    30  1.0n
M2 1    8   6    6   INTER
E2 8    6   4    1   2
.MODEL INTER PMOS (VTO=0   KP=10   LEVEL=1)
Cgdmax  7  4    950p
Rcgd    7  4    10meg
Dgd     4  6    DGD
Rdgd    4  6    10meg
.MODEL DGD D(M=0.50   CJO=950p    VJ=0.52)
M3      7  9  1  1  INTER
E3      9  1  4  1  -2
.ENDS FQD17P06
*-------------------------------------------------------------------------------------
*FAIRCHILD      CASE: D-PAK      PID: FQD17P06 
*NOV-18-2000 CREATION    
